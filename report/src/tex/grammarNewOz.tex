%! Author = Martin Vandenbussche
\begin{lstlisting}[label={lst:newOzEBNF},language=ebnf]
Note that the concatenation symbol in EBNF (comma) is
omitted for readability reasons
Notation      Meaning
===========================================================================
;\epsilon;             singleton containing the empty word
;$(w)$;           grouping of regular expressions
;$[w]$;           union of ;\epsilon; with the set of words ;$w$; (optional group)
;$\{w\}$;          zero or more times ;$w$;
;$\{w\}+$;         one or more times ;$w$;
;$w_1~w_2$;        concatenation of ;$w_1$; with ;$w_2$;
;$w_1 | w_2$;         logical union of ;$w_1$; and ;$w_2$; (OR)
;$w_1-w_2$;       difference of ;$w_1$; and ;$w_2$;

interStatement ::= {nestConStatement}+
            | DECLARE inStatement EOF

statement ::= nestConStatement
            | SKIP

expression ::= nestConExpression
            | nestDecAnonym
            | DOLLAR
            | term
            | THIS
            | inExpression

parExpression ::= LPAREN expression RPAREN

//Declarations still need to come first (keeps Oz' idea)
inStatement ::= LCURLY {declarationPart} {statement} RCURLY

inExpression ::= LCURLY {declarationPart} {statement} [expression] RCURLY

nestConStatement ::= assignmentExpression
            | variable LPAREN [expression {COMMA expression}] RPAREN
            | inStatement
            | IF parExpression inStatement
              {ELSE IF LPAREN expression RPAREN inStatement}
              [ELSE inStatement]
            | MATCH expression LCURLY
                {CASE caseStatementClause}+
                [ELSE inStatement]
              RCURLY
            | FOR LPAREN {loopDec}+ RPAREN inStatement
            | TRY inStatement
              [CATCH LCURLY
                {CASE caseStatementClause}+
              RCURLY]
              [FINALLY inStatement]
            | RAISE inExpression
            | THREAD inStatement
            | LOCK [LPAREN expression RPAREN] inStatement

nestConExpression ::= IF LPAREN expression RPAREN inExpression
                {ELSE IF LPAREN expression RPAREN inExpression}
                [ELSE inExpression]
            | MATCH expression LCURLY
               {CASE caseExpressionClause}+
               [ELSE inExpression]
              RCURLY
            | TRY inExpression
              [CATCH LCURLY
                {CASE caseExpressionClause}+
              RCURLY]
              [FINALLY inStatement]
            | RAISE inExpression
            | THREAD inExpression

nestDecVariable ::= DEFPROC variable LPAREN [pattern {COMMA pattern}] RPAREN inStatement
            | DEF [LAZY] variable LPAREN [pattern {COMMA pattern}] RPAREN inExpression
            | FUNCTOR [variable] {
                (IMPORT importClause {COMMA importClause}+)
                | (EXPORT exportClause {COMMA exportClause}+)
              }
              inStatement
            | CLASS variableStrict {classDescriptor} LCURLY
              {classElementDef} RCURLY

nestDecAnonym ::= DEFPROC DOLLAR LPAREN [pattern {COMMA pattern}] RPAREN inStatement
            | DEF [LAZY] DOLLAR LPAREN [pattern {COMMA pattern}] RPAREN inExpression
            | FUNCTOR [DOLLAR] {
                (IMPORT importClause {COMMA importClause}+)
                | (EXPORT exportClause {COMMA exportClause}+)
              }
              inStatement
            | CLASS DOLLAR {classDescriptor} LCURLY
              {classElementDef} RCURLY

importClause ::=  variable
                    [LPAREN (atom|int)[COLON variable] {COMMA (atom|int)[COLON variable]} RPAREN]
                    [FROM atom]

exportClause ::= [(atom|int) COLON] variable

classElementDef ::= DEFPROC methHead [ASSIGN variable] (inExpression|inStatement)
            | classDescriptor

caseStatementClause ::= pattern {(LAND|LOR) conditionalExpression} IMPL inStatement

caseExpressionClause ::= pattern {(LAND|LOR) conditionalExpression} IMPL inExpression

assignmentExpression ::= conditionalExpression

assignmentStatement ::= variable ASSIGN expression

conditionalExpression ::= conditionalOrExpression

conditionalOrExpression ::= conditionalAndExpression {LOR conditionalAndExpression}

conditionalAndExpression ::= equalityExpression {LAND equalityExpression}

equalityExpression ::= relationalExpression {EQUAL relationalExpression}

relationalExpression ::= additiveExpression [(GT|GE|LT|LE) additiveExpression]

additiveExpression ::= multiplicativeExpression {(PLUS|MINUS) multiplicativeExpression}

multiplicativeExpression ::= unaryExpression {(STAR|SLASH|MODULO) unaryExpression}

unaryExpression ::= (MINUS|PLUS) unaryExpression
            | simpleUnaryExpression

simpleUnaryExpression ::= LNOT unaryExpression
            | postfixExpression

postfixExpression ::= primary

primary ::= variable | int | float | character | string | UNIT | TRUE | FALSE | UNDERSCORE | NIL
            | variable LPAREN [expression {COMMA expression}] RPAREN
            | variable DOT variable [LPAREN [expression {COMA expression}] RPAREN]
            | THIS DOT variable LPAREN [expression {COMMA expresssion}] RPAREN
            | SUPER [LPAREN variableStrict RPAREN]
                DOT variable LPAREN [expression {COMMA expression}] RPAREN
            | parExpression

term ::= assignmentExpression
            | atomLisp LPAREN [[feature COLON]pattern {COMMA [feature COLON]pattern} [COMMA ELLIPSIS]] RPAREN
            | LPAREN expression {HASHTAG expression}+ RPAREN
            | LPAREN expression {COLCOL expression}+ RPAREN
            | LBRACK [expression {COMMA expression}] RBRACK

pattern ::= variable | int | float | character | string | UNIT | TRUE | FALSE | UNDERSCORE | NIL
            | atomLisp LPAREN [[feature COLON]pattern {COMMA [feature COLON]pattern} [COMMA ELLIPSIS]] RPAREN
            | LPAREN pattern {HASHTAG pattern}+ RPAREN
            | LPAREN pattern {COLCOL pattern}+ RPAREN
            | LBRACK [pattern {COMMA pattern}] RBRACK
            | LPAREN pattern RPAREN

declarationPart ::= (VAL|VAR) variable [ASSIGN expression]
                      {COMMA variable [ASSIGN expression]}
            | nestDecVariable

loopDec ::= variable IN expression DOTDOT expression [SEMI expression]
            | variable IN expression SEMI expression [SEMI expression]
            | variable IN expression

feature ::= atomLisp

classDescription ::= EXTENDS variableStrict {COMMA variableStrict}+
            | ATTR variable [ASSIGN expression]

methHead ::= (variableStrict | variable)
              LPAREN [methArg {COMMA methArg} [COMMA ELLIPSIS]] RPAREN

methArg ::= [feature COLON] (variable | UNDERSCORE) [LE expression]

\end{lstlisting}
