%! Author = Martin Vandenbussche
\begin{lstlisting}[label={lst:newOzLexical},language=ebnf]
Notation      Meaning
===========================================================================
;\epsilon;             singleton containing the empty word
;$(w)$;           grouping of regular expressions
;$[w]$;            union of ;\epsilon; with the set of words ;$w$; (optional group)
;$\{w\}$;           zero or more times ;$w$;
;$\{w\}+$;         one or more times ;$w$;
;$w_1~w_2$;         concatenation of ;$w_1$; with ;$w_2$;
;$w_1 | w_2$;         logical union of ;$w_1$; and ;$w_2$; (OR)
;$w_1-w_2$;       difference of ;$w_1$; and ;$w_2$;

// White spaces - ignored
WHITESPACE ::= (" "|"\b"|"\t"|"\n"|"\r"|"\f")

// Comments - ignored
("//" {~("\n"|"\r")} ("\n"|"\r"|"\r\n")
// Multi-line comments - ignored
"/*" {CHAR - "*/"} "*/"

// Reserved keywords
AT      ::= "at"
ATTR    ::= "attr"
BREAK   ::= "break"
CASE    ::= "case"
CATCH   ::= "catch"
CLASS   ::= "class"
CONTINUE::= "continue"
DECLARE ::= "declare"
DEF     ::= "def"
DEFPROC ::= "defproc"
DO      ::= "do"
ELSE    ::= "else"
EXPORT  ::= "export"
EXTENDS ::= "extends"
FALSE   ::= "false"
FINALLY ::= "finally"
FOR     ::= "for"
FROM    ::= "from"
FUNCTOR ::= "functor"
IF      ::= "if"
IMPORT  ::= "import"
IN      ::= "in"
LAZY    ::= "lazy"
LOCK    ::= "lock"
MATCH   ::= "match"
NIL     ::= "nil"
OR      ::= "or"
PROP    ::= "prop"
RAISE   ::= "raise"
RETURN  ::= "return"
SKIP    ::= "skip"
SUPER   ::= "super"
THIS    ::= "this"
THREAD  ::= "thread"
TRUE    ::= "true"
TRY     ::= "try"
UNIT    ::= "unit"
VAL     ::= "val"
VAR     ::= "var"

// Operators
ASSIGN      ::= "="
PLUSASS     ::= "+="
MINUSASS    ::= "-="
EQUAL       ::= "=="
NE          ::= "\\="
LT          ::= "<"
GT          ::= ">"
LE          ::= "<="
GE          ::= ">="
IMPL        ::= "=>"
LAND        ::= "&&"
LOR         ::= "||"
LNOT        ::= "!"
MINUS       ::= "-"
PLUS        ::= "+"
STAR        ::= "*"
SLASH       ::= "/"
MODULO      ::= "%"
HASHTAG     ::= "#"
UNDERSCORE  ::= "_"
DOLLAR      ::= "$"
APOSTROPHE  ::= "'"
QUOTE       ::= "\""
DEGREE      ::= "°"
COLCOL      ::= "::"
COMMA       ::= ","
LBRACK      ::= "["
LCURLY      ::= "{"
LPAREN      ::= "("
RBRACK      ::= "]"
RCURLY      ::= "}"
RPAREN      ::= ")"
SEMI        ::= ";;"
COLON       ::= ":"
DOT         ::= "."
DOTDOT      ::= ".."
ELLIPSIS    ::= "..."

// Literals
VARIABLESTRICT ::= UPPERCASE{ALPHANUM}
                    | "`"(ESC | PSEUDO_CHAR | ~("`"|"\\"|"\n"|"\r") )"`")
VARIABLE       ::= LOWERCASE{ALPHANUM}
ATOM           ::= (ATOMLISP | "´" (ESC | PSEUDO_CHAR | ~("\\"|"\n"|"\r") ) "´")
ATOMLISP       ::= "'" {LETTER}
STRING         ::= "\"" { ESC | PSEUDO_CHAR | ~("\""|"\\"|"\n"|"\r") } "\""
CHARACTER      ::= (DEGREE(CHARCHAR | PSEUDO_CHAR)
                    | "'" (ESC | ~("'"|"\\"|"\n"|"\r") ) "'" )
INT            ::= (DECINT | HEXINT | OCTINT | BININT)
FLOAT          ::= {DIGIT}+ DOT {DIGIT} [ ("e"|"E")["~"]{DIGIT}+ ]
UPPERCASE      ::= "A"|...|"Z"
LOWERCASE      ::= "a"|...|"z"
LETTER         ::= "A"|...|"Z"|"a"|...|"z"
DIGIT          ::= "0"|...|"9"
NON_ZERO_DIGIT ::= "1"|...|"9"
CHARINT        ::= ("0"|...|"9") | ("1"|...|"9")("0"|...|"9")
                    | "1"("0"|...|"9")("0"|...|"9")
                    | "2"("0"|...|"4")("0"|...|"9")|"25"("0"|...|"5") // (0-255)
ALPHANUM       ::= (UPPERCASE | LOWERCASE | DIGIT | UNDERSCORE)
DECINT         ::= ("0" | (NON_ZERO_DIGIT{DIGIT}))
HEXINT         ::= "0" ("x"|"X") {HEXDIGIT}+
OCTINT         ::= "0" {OCTDIGIT}+
BININT         ::= "0" ("b"|"B") {BINDIGIT}+
OCTDIGIT       ::= "0"|...|"7"
HEXDIGIT       ::= (DIGIT | ("A"|...|"F") | ("a"|...|"f"))
BINDIGIT       ::= ("0"|"1")
ESC            ::= "\\" ESCAPE_CHAR
ESCAPE_CHAR    ::= ("a"|"b"|"f"|"n"|"r"|"t"|"\\"|"'"|"\""|DEGREE)
CHARCHAR       ::= ~("\\")
// In the classes of words <variable>, <atom>, <string>, and <character>, we use pseudo-characters, which represent single characters in different notations.
PSEUDO_CHAR    ::= ( "\\"(OCTDIGIT)(OCTDIGIT)(OCTDIGIT) ) | ( "\\"("x"|"X")(HEXDIGIT)(HEXDIGIT) )
// End of file
EOF            ::= "<end of file>"

\end{lstlisting}